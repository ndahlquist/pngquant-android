apply plugin: 'com.android.library'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // Disable the built-in NDK task (this is too rigid for our purposes).
    // TODO: This was written in 2017. It's likely the modern build system would be sufficient.
    sourceSets.main.jni.srcDirs = []

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn customNdkBuild
    }
}

task customNdkBuild(type: Exec) {
    description 'Build our native code.'
    workingDir '.'
    commandLine './build_native_libs.sh'
    dependsOn 'installNdk'
}

task installNdk(type: Exec) {
    description 'Creates a local install of the Android Native Development Kit'
    workingDir '.'
    commandLine './install_ndk.sh'
}
